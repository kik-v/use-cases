PREFIX vph-pers: <http://purl.org/ozo/vph-pers#>
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>

SELECT (COUNT(DISTINCT ?personeel_bepaalde_tijd) AS ?teller) (COUNT(DISTINCT ?personeel) AS ?noemer) (?teller/?noemer*100 AS ?indicator)

WHERE { 
    #Definieer peildatum
    BIND("2019-07-01"^^xsd:date AS ?peildatum)

    {
        #Selecteer personen met arbeidsovereenkomst bepaalde tijd
        ?bepaalde_tijd a vph-pers:ArbeidsOvereenkomstBepaaldeTijd ;
                       vph-pers:startDatum ?start_overeenkomst ;
                       vph-pers:eindDatum ?eind_overeenkomst ;
                       vph-pers:bijbehorendeFunctie/a vph-pers:ZorgverlenerFunctie ;
                       vph-pers:heeftOpdrachtnemer ?personeel_bepaalde_tijd .
    } UNION {
        #Selecteer personen met een arbeidsovereenkomst
        ?arbeidsovereenkomst a vph-pers:ArbeidsOvereenkomst ;
                             vph-pers:startDatum ?start_overeenkomst ;
                             vph-pers:bijbehorendeFunctie/a vph-pers:ZorgverlenerFunctie ;
                             vph-pers:heeftOpdrachtnemer ?personeel .
        OPTIONAL {?arbeidsovereenkomst vph-pers:eindDatum ?eind_overeenkomst}
    }
    
    #Alleen overeenkomsten die geldig zijn op peildatum
    FILTER (?start_overeenkomst <= ?peildatum && (!BOUND(?eind_overeenkomst) || ?eind_overeenkomst >= ?peildatum))
}