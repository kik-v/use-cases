PREFIX vph-pers: <http://purl.org/ozo/vph-pers#>
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>

SELECT (SUM(?omvang_corr) AS ?teller) (COUNT (DISTINCT(?persoon)) AS ?noemer) (?teller/?noemer AS ?indicator)

WHERE {
    #definieer periode waarin overeenkomst geldig moet zijn en de eenheid waarin gerapporteerd wordt
    BIND("2020-01-01"^^xsd:date AS ?start_periode)
    BIND("2020-12-31"^^xsd:date AS ?eind_periode)
    BIND(vph-pers:fte_36 AS ?gevraagde_eenheid)
    BIND ((360 * (YEAR(?eind_periode) - YEAR(?start_periode))) +
        (30 * (MONTH(?eind_periode) - MONTH(?start_periode))) +
        IF((DAY(?eind_periode) - DAY(?start_periode)) < 29, (DAY(?eind_periode) - DAY(?start_periode)), 29) + 1
        AS ?dagen_periode)
    
    #selecteer alle arbeids- en oproepovereenkomsten met een zorgverlenerfunctie
    ?arbeidsovereenkomst a vph-pers:ArbeidsOvereenkomst .
    ?arbeidsovereenkomst vph-pers:bijbehorendeFunctie/a vph-pers:Functie ;
                         vph-pers:heeftContractOmvang ?omvang ;
                         vph-pers:heeftOpdrachtnemer ?persoon .
    
    #bepaal van alle arbeidsovereenkomsten de omvang
    ?omvang vph-pers:numeriekeWaarde ?waarde ;
            vph-pers:hasUnitOfMeasure ?eenheid ;
            vph-pers:startDatum ?start_omvang .
    OPTIONAL {?omvang vph-pers:eindDatum ?eind_omvang}
    
    #alleen contractomvang meenemen binnen gevraagde periode
    FILTER (?start_omvang <= ?eind_periode && (!BOUND(?eind_omvang) || ?eind_omvang >= ?start_periode))
    BIND(IF(?start_periode > ?start_omvang, ?start_periode, ?start_omvang) AS ?start_omvang_corr)
    BIND(IF((?eind_periode < ?eind_omvang) || !BOUND(?eind_omvang), ?eind_periode, ?eind_omvang) AS ?eind_omvang_corr)
    
    #bepaal aantal dagen binnen de gevraagde periode dat de omvang meetelt
    BIND ((360 * (YEAR(?eind_omvang_corr) - YEAR(?start_omvang_corr))) +
        (30 * (MONTH(?eind_omvang_corr) - MONTH(?start_omvang_corr))) +
        IF((DAY(?eind_omvang_corr) - DAY(?start_omvang_corr)) < 29, (DAY(?eind_omvang_corr) - DAY(?start_omvang_corr)), 29) + 1
        AS ?dagen_omvang_corr)
    
    #bepaal correct voor contracten die maar deels geldig zijn in de periode
    BIND(?dagen_omvang_corr/?dagen_periode AS ?correctie_periode)
    
    #correct voor verschillende eenheden
    ?gevraagde_eenheid vph-pers:numeriekeWaarde ?gevraagde_eenheid_waarde ;
                                    vph-pers:hasDenominatorQualityValue ?noemer_eenheid ;
                                    vph-pers:hasNumeratorQualityValue ?teller_eenheid .
    ?eenheid vph-pers:numeriekeWaarde ?eenheid_waarde ;
                  vph-pers:hasDenominatorQualityValue ?noemer_eenheid ;
                  vph-pers:hasNumeratorQualityValue ?teller_eenheid .
    BIND(?eenheid_waarde/?gevraagde_eenheid_waarde AS ?correctie_factor)
    BIND(?waarde * ?correctie_factor * ?correctie_periode AS ?omvang_corr)
}