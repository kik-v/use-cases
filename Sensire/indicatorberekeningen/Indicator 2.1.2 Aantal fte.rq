PREFIX vph-g: <http://purl.org/ozo/vph-g#>
PREFIX vph-pers: <http://purl.org/ozo/vph-pers#>
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>

SELECT ?start_periode ?eind_periode (SUM(?aantal) AS ?totaal_aantal) ?eenheid

WHERE {
    {
        #selecteer werknemers met een arbeidsovereenkomst of oproepovereenkomst in de gevraagde periode
        SELECT DISTINCT ?start_periode ?eind_periode ?werknemer
        WHERE {
            BIND ("2020-01-01"^^xsd:date AS ?start_periode)
            BIND ("2020-12-31"^^xsd:date AS ?eind_periode)  
            ?overeenkomst a vph-pers:ArbeidsOvereenkomst ;
                       vph-pers:startDatum ?start_overeenkomst .
            OPTIONAL {?overeenkomst vph-pers:eindDatum ?eind_overeenkomst}
            FILTER(?start_overeenkomst <= ?eind_periode && ((?eind_overeenkomst >= ?start_periode) || (!BOUND(?eind_overeenkomst))))
            ?werknemer vph-pers:heeftWerkOvereenkomst ?overeenkomst .
            ?overeenkomst vph-pers:bijbehorendeFunctie/a vph-pers:ZorgverlenerFunctie . #Alleen overeenkomsten met zorgverlener functies
         }
    }
    ?werknemer vph-pers:heeftGewerkt ?periode .
    ?periode vph-pers:startDatum ?start_gewerkt ;
             vph-pers:eindDatum ?eind_gewerkt ;
             vph-pers:gewerkteUren ?uren_gewerkt .
    
    #Alleen gewerktmeldingen binnen de gevraagde periode
    FILTER(?start_gewerkt >= ?start_periode && ?eind_gewerkt <= ?eind_periode)

    #Rapporteer op alle eenheden per week
    ?e a vph-g:UnitOfWorkload ;
       vph-pers:numeriekeWaarde ?corr_factor ;
       vph-pers:hasDenominatorQualityValue vph-g:Week ;
       rdfs:label ?eenheid .
    
    BIND ((360 * (YEAR(?eind_periode) - YEAR(?start_periode))) +
        (30 * (MONTH(?eind_periode) - MONTH(?start_periode))) +
        IF((DAY(?eind_periode) - DAY(?start_periode)) < 29, (DAY(?eind_periode) - DAY(?start_periode)), 29) + 1
        AS ?dagen_periode)
    BIND (?uren_gewerkt/(?dagen_periode/360*47)/?corr_factor AS ?aantal)
}
GROUP BY ?start_periode ?eind_periode ?eenheid