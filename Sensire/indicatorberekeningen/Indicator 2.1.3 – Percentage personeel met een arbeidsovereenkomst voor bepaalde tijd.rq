PREFIX kik: <https://purl.org/ozo/kik#>
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>

SELECT (COUNT(DISTINCT ?personeel_bepaalde_tijd) AS ?teller) (COUNT(DISTINCT ?personeel) AS ?noemer) (?teller/?noemer*100 AS ?indicator)

WHERE { 
    #Definieer peildatum
    BIND("2018-12-01"^^xsd:date AS ?peildatum)

    {
        #Selecteer personen met arbeidsovereenkomst bepaalde tijd
        ?bepaalde_tijd a kik:ArbeidsOvereenkomstBepaaldeTijd ;
                       kik:startDatum ?start_overeenkomst ;
                       kik:eindDatum ?eind_overeenkomst ;
                       ^kik:heeftWerkOvereenkomst ?personeel_bepaalde_tijd .
    } UNION {
        #Selecteer personen met een arbeidsovereenkomst
        ?arbeidsovereenkomst a kik:ArbeidsOvereenkomst ;
                             kik:startDatum ?start_overeenkomst ;
                             ^kik:heeftWerkOvereenkomst ?personeel .
        OPTIONAL {?arbeidsovereenkomst kik:eindDatum ?eind_overeenkomst}
    }
    
    #Alleen overeenkomsten die geldig zijn op peildatum
    FILTER (?start_overeenkomst <= ?peildatum && (!BOUND(?eind_overeenkomst) || ?eind_overeenkomst >= ?peildatum))
}
